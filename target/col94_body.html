<h1><a name="linux_magazine_column_94__jun_2007_">Columna 94 del Linux Magazine (juny de 2007)</a></h1>
<p>[títol suggerit: «El Moose està volant (1a part)»]</p>
<p>El sistema d'objectes de Perl és molt «flexible», és a dir, es construeix de baix cap a dalt. Podeu construir objectes tradicionals basats en taules de dispersió, o objectes més exòtics basats en matrius o objectes capgirats. Aleshores heu de crear els mètodes accessors, definir les polítiques d'accés i generar un munt de codi repetit.</p>
<p>Afortunadament, Perl és prou introspectiu com perquè li encomaneu la majoria del treball dur i avorrit. Això ha provocat que un bon nombre «d'entorns de treball per a classes» hagin estat publicats al CPAN. L'enton de treball <code>Moose</code> va sorgir fa un any i inicialment jo el vaig descartar com «un altre entorn de treball per a classes», de la mateixa manera que un altre sistema de plantilles o un altre mapeig d'objectes relacional em fan sentir.</p>
<p>Tanmateix, vaig fer un cop d'ull recentment al que s'havia convertit <code>Moose</code> i em vaig endur una sorpresa agradable. Així que em vaig posar a jugar-hi, sovint vaig exclamar que aquest entorn de treball m'hauria estalviat un bona pila de temps en alguns projectes passats, com ara el text que vaig escriure per al nostre curs i llibre <em>Intermediate Perl</em>, algunes parts del qual s'han inclòs com la pàgina de manual <em>perlboot</em> a la distribució. Posem-nos a refer les classes «d'animals» d'aquell text utilitzant <code>Moose</code> per veure com aquest entorn de treball emergent simplifica les coses.</p>
<p>Primer crearem la classe cavall a <code>Horse.pm</code> que té un nom i un color:</p>
<pre>
  package Horse;
  use Moose;
  has 'name' =&gt; (is =&gt; 'rw');
  has 'color' =&gt; (is =&gt; 'rw');
  1;</pre>
<p>Incorporant <code>Moose</code> es defineix <code>has</code>, al qual se li passa el nom d'un atribut juntament amb les seves propietats. En aquest cas, diem que els dos atributs són de «lectura/escriptura». Ara podem utilitzar aquesta classe:</p>
<pre>
  use Horse;
  my $talking = Horse-&gt;new(name =&gt; "Mr. Ed");
  print $talking-&gt;name; # prints Mr. Ed
  $talking-&gt;color("grey"); # sets the color</pre>
<p>Fixeu-vos que no m'ha calgut definir un mètode <code>new</code>: Moose ho fa per mi.</p>
<p>En el text original <code>Horse</code> heretava d'<code>Animal</code>. Podem fer això molt fàcilment. Posem a <code>Animal.pm</code>:</p>
<pre>
  package Animal;
  use Moose;
  has 'name' =&gt; (is =&gt; 'rw');
  has 'color' =&gt; (is =&gt; 'rw');
  1;</pre>
<p>I aleshores actualitzem el nostre <code>Horse.pm</code>:</p>
<pre>
  package Horse;
  use Moose;
  extends 'Animal';
  1;</pre>
<p>Fixeu-vos que aquí <code>extends</code> substitueix el tradicional <code>use base</code> i fixa completament <code>@ISA</code>, enlloc d'afegir-hi un element. (És possible que vulgueu posar això dins d'un bloc BEGIN, tot i que encara no he vist cap exemple que ho necessiti).</p>
<p>En aquest punt, <code>Horse</code> i <code>Animal</code> són idèntics. Els dos es poden instanciar i se'ls pot assignar atributs. En l'exemple original, el que distingia un cavall era el so que feia, cosa que podem afegir aquí:</p>
<pre>
  package Horse;
  use Moose;
  extends 'Animal';
  sub sound { 'neigh' }
  1;</pre>
<p>i aleshores fer-hi referència al mètode comú <code>speak</code> d'<code>Animal</code>:</p>
<pre>
  package Animal;
  use Moose;
  has 'name' =&gt; (is =&gt; 'rw');
  has 'color' =&gt; (is =&gt; 'rw');
  sub speak {
    my $self = shift;
    print $self-&gt;name, " goes ", $self-&gt;sound, "\n";
  }
  sub sound { confess shift, " should have defined sound!" }
  1;</pre>
<p>Fixeu-vos en l'ús de <code>confess</code>, un altre regal de Moose. Si la classe derivada no ha definit un so, vull queixar-me. Però com que <code>Horse</code> defineix <code>sound</code>, no veure mai això per un cavall. Amb aquest codi puc crear el meu clàssic cavall parlador:</p>
<pre>
  my $talking = Horse-&gt;new(name =&gt; 'Mr. Ed');
  $talking-&gt;speak; # says "Mr. Ed goes neigh"</pre>
<p>Fins ara només estic programant coses que serien senzilles sense Moose, així que comencem a fer alguns canvis per veure'n tot el poder. Primer, un <code>Animal</code> és en realitat una classe abstracta, utilitzada només per proveïr atributs i mètodes comuns per a una classe concreta (en aquest cas, la classe cavall). En la terminologia de Moose això es descriu millor com un <em>rol</em>. Un rol és com un <em>mix-in</em>, que proveeix una col·lecció d'atributs i mètodes que utilitzen aquells atributs. Un rol mai té cap instància perquè no és una classe completa.</p>
<p>Quan fem que <code>Animal</code> sigui un rol també obtenim suport addicional:</p>
<pre>
  package Animal;
  use Moose::Role;
  has 'name' =&gt; (is =&gt; 'rw');
  has 'color' =&gt; (is =&gt; 'rw');
  sub speak {
    my $self = shift;
    print $self-&gt;name, " goes ", $self-&gt;sound, "\n";
  }
  requires 'sound';
  1;</pre>
<p>Fixeu-vos que hem substituït l'«stub» que incloïa el confess per un <code>requires</code>. Això informa a Moose que les classes que usin aquest rol han de proveir el mètode <code>sound</code>, que serà verificat en temps de compilació. Per atorgar un rol utilitzem <code>with</code> enlloc d'<code>extends</code>:</p>
<pre>
  package Horse;
  use Moose;
  with 'Animal';
  sub sound { 'neigh' }
  1;</pre>
<p>Si ens haguéssim oblidat d'incloure <code>sound</code> hauríem rebut un avís de bon començament. Genial. En aquest punt, <code>Horse</code> segueix funcionant com abans.</p>
<p>Què passa amb les paraules clau <code>with</code> i <code>requires</code>? Com que estan definits per les importacions de <code>Moose</code> i <code>Moose::Role</code>, romandran com a part del paquet. Per als puristes com nosaltres als que no ens agrada aquest tipus de pol·lució, podem eliminar-los quan hàgim acabat utilitzant la paraula clau <code>no</code> (de forma similar a <code>use strict</code> i <code>no strict</code>). Per exemple, podem netejar <code>Horse.pm</code> amb:</p>
<pre>
  package Horse;
  use Moose;
  with 'Animal';
  sub sound { 'neigh' }
  no Moose; # gets rid of scaffolding
  1;</pre>
<p>I de forma similar, <code>Animal.pm</code> necessita <code>no Moose::Role</code> al final.</p>
<p>Moose ofereix suport per a la noció de valor predeterminat. Afegim el color predeterminat i fem que això també sigui responsabilitat de la classe:</p>
<pre>
  package Animal;
  ...
  has 'color' =&gt; (is =&gt; 'rw', default =&gt; sub { shift-&gt;default_color });
  requires 'default_color';
  ...</pre>
<p>Si no s'indica el color, es consultarà el color predeterminat de la classe i <code>requires</code> garanteix que la classe concreta proveeix aquest color predeterminat. Les nostres classes derivades ara tenen aquest aspecte:</p>
<pre>
  ## Cow.pm:
  package Cow;
  use Moose;
  with 'Animal';
  sub default_color { 'spotted' }
  sub sound { 'moooooo' }
  no Moose;
  1;
  ## Horse.pm:
  package Horse;
  use Moose;
  with 'Animal';
  sub default_color { 'brown' }
  sub sound { 'neigh' }
  no Moose;
  1;
  ## Sheep.pm:
  package Sheep;
  use Moose;
  with 'Animal';
  sub default_color { 'black' }
  sub sound { 'baaaah' }
  no Moose;
  1;</pre>
<p>Ara «ovella» és una més de les nostres classes implementades:</p>
<pre>
  use Sheep;
  my $baab = Sheep-&gt;new(color =&gt; 'white', name =&gt; 'Baab');
  $baab-&gt;speak; # prints "Baab goes baaaah"</pre>
<p>Bé, això és força pim pam. Anem a resoldre alguns dels altres problemes del material original.</p>
<p>La classe <code>Mouse</code> era especial perquè extenia el mètode <code>speak</code> amb una línia addicional de sortida. Tot i que podríem utilitzar els mètodes tradicionals basats en <code>SUPER::</code> per cridar els comportaments de la classe pare, això no funciona amb els rols. (Els rols no acaben dins de <code>@ISA</code> perquè estan «empegats per dins» enlloc d'«enganxats al damunt»).</p>
<p>Afortunadament, Moose ofereix convenientment la crida <code>after</code> per afegir passes addicionals al final d'una subrutina existent. Moose fa això substituint la subrutina original per una nova subrutina que crida l'orginal i tot seguit crida al codi addicional. El context (llista, escalar, o buit) es conserva de forma adequada, així com també el valor de retorn original. El nostre <code>speak</code> retocat té aquesta pinta:</p>
<pre>
  package Mouse;
  use Moose;
  with 'Animal';
  sub default_color { 'white' }
  sub sound { 'squeak' }
  after 'speak' =&gt; sub {
    print "[but you can barely hear it!]\n";
  };
  no Moose;
  1;</pre>
<p>Això ens dóna un ratolí que funciona correctament:</p>
<pre>
  my $mickey = Mouse-&gt;new(name =&gt; 'Mickey');
  $mickey-&gt;speak;</pre>
<p>que dóna com a resultat:</p>
<pre>
  Mickey goes squeak
  [but you can barely hear it!]</pre>
<p>També podem utilitzar <code>before</code> per a precedir el comportament original o <code>around</code> per a controlar com es crida el comportament original, segons calgui. Per exemple, per a permetre que <code>name</code> s'utilitzi com un mètode accessor però que segueixi retornant <code>an unnamed Horse</code> quan s'utilitzi com un mètode de classe, podem posar un «around» del mètode accessor resultant:</p>
<pre>
  package Animal;
  ...
  has 'name' =&gt; (is =&gt; 'rw');
  around 'name' =&gt; sub {
    my $next = shift;
    my $self = shift;
    blessed $self ? $self-&gt;$next(@_) : "an unnamed $self";
  };</pre>
<p>El <code>has</code> crear el comportament original. L'<code>around</code> intercepta el nom de la subroutina original, fent que la referència al codi original es passi com a primer paràmetre d'aquesta nova subrutina, que nosaltres capturem a <code>$next</code>. Desencuem el <code>$self</code> original i el verifiquem per veure si és un objecte o no, via <code>blessed</code> (exportat per Moose convenientment). Per a un objecte obtenim el comportament original (un «getter» o «setter»), però per a una classe obtindrem la cadena literal.</p>
<p>Què passa si no li posem un nom al nostre animal? Obtindrem avisos sobre valors indefinits. Podem assignar un nom predeterminat tal com vam fer amb el color predeterminat:</p>
<pre>
  has 'name' =&gt; (
    is =&gt; 'rw',
    default =&gt; sub { 'an unnamed ' . ref shift },
   );</pre>
<p>Novament, voldríem aquell <code>around</code> immediatament després d'aquest pas.</p>
<p>Si no volem que la gent canviï el color després de la creació inicial de la instància, declarem l'atribut no modificable:</p>
<pre>
  has 'color' =&gt; (is =&gt; 'ro', default =&gt; sub { shift-&gt;default_color });</pre>
<p>Ara un intent de canviar el color s'avortarà amb <code>Cannot assign a value to a read-only accessor...</code>. Si realment volíem una manera de canviar el color ocasionalment, podem definir a banda un anomenat <em>escriptor</em>:</p>
<pre>
  has 'color' =&gt; (
    is =&gt; 'ro',
    writer =&gt; 'private_set_color',
    default =&gt; sub { shift-&gt;default_color },
  );</pre>
<p>D'aquesta manera no podem canviar el color d'un ratolí directament:</p>
<pre>
  my $m = Mouse-&gt;new;
  my $color = $m-&gt;color; # gets the color
  $m-&gt;color('green'); # DIES</pre>
<p>Però en canvi podem utilitzar un mètode privat:</p>
<pre>
  $m-&gt;private_set_color('green'); # sets the color to green</pre>
<p>Tot utilitzant un nom llarg és menys probable que el cridem accidentalment, a menys que tinguem la intenció de canviar el color.</p>
<p>Anem a crear un <code>RaceHorse</code> afegint «característiques de carrera» a un <code>Horse</code>.</p>
<p>Primer definim «característiques de carrera» com, sí, un altre rol:</p>
<pre>
  package Racer;
  use Moose::Role;
  has $_ =&gt; (is =&gt; 'rw', default =&gt; 0)
    foreach qw(wins places shows losses);
  no Moose::Role;
  1;</pre>
<p>Com que <code>has</code> és només una crida a una subrutina, fixeu-vos que podem utilitzar estructures de control tradicionals de Perl (aquí, un bucle <code>foreach</code>). Amb una mica de codi hem afegit uns altres quatre atributs. El valor inicial <code>0</code> significa que no ens caldrà escriure per separat codi d'inicialització al nostre constructor. Tot seguit podem afegir alguns mètodes accessors:</p>
<pre>
  package Racer;
  ...
  sub won { my $self = shift; $self-&gt;wins($self-&gt;wins + 1) }
  sub placed { my $self = shift; $self-&gt;places($self-&gt;places + 1) }
  sub showed { my $self = shift; $self-&gt;shows($self-&gt;shows + 1) }
  sub lost { my $self = shift; $self-&gt;losses($self-&gt;losses + 1) }</pre>
<pre>
  sub standings {
    my $self = shift;
    join ", ", map { $self-&gt;$_ . " $_" } qw(wins places shows losses);
  }
  ...</pre>
<p>Cada crida a <code>won</code> incrementa el número de victòries. Això seria més senzill si assumíssim que aquests objectes s'implementen amb taules de dispersió (per defecte és així), com ara:</p>
<pre>
  sub won { shift-&gt;{wins}++; }</pre>
<p>Tanmateix, utilitzant la interfície pública (una crida al mètode) podríem canviar més endavant la implementació amb objectes capgirats o objectes basats en matrius sense trencar aquest codi. Això és especialment important quan es crea un rol genèric, que es podria mesclar amb qualsevol tipus d'objecte.</p>
<p>Per a crear la carrera de cavalls només cal que mesclem un cavall amb un corredor:</p>
<pre>
  package RaceHorse;
  use Moose;
  extends 'Horse';
  with 'Racer';
  no Moose;
  1;</pre>
<p>I ara ja podem cavalcar els ponis:</p>
<pre>
  use RaceHorse;
  my $s = RaceHorse-&gt;new(name =&gt; 'Seattle Slew');
  $s-&gt;won; $s-&gt;won; $s-&gt;won; $s-&gt;placed; $s-&gt;lost; # run some races
  print $s-&gt;standings, "\n"; # 3 wins, 1 places, 0 shows, 1 losses</pre>
<p>Fins ara només he rascat la superfície del que proporciona <code>Moose</code>. El proper mes miraré algunes de les característiques més avançades de <code>Moose</code> que ajuden a mantenir les coses complexes de manera relativament simple. Fins aleshores, gaudiu!</p>

